apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'com.apollographql.android'

apollo {
    nullableValueType = 'apolloOptional'
}

android {
    compileSdkVersion 28
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api 'com.android.support:appcompat-v7:28.0.0'
    api 'com.android.support.constraint:constraint-layout:1.1.3'

    def lifecycle_version = "1.1.0"
    api "android.arch.lifecycle:extensions:$lifecycle_version"
    kapt "android.arch.lifecycle:compiler:$lifecycle_version"

    api "com.shopify:livedata-ktx:1.0.1"

    testImplementation 'junit:junit:4.12'

    api "com.apollographql.apollo:apollo-runtime:$apollo_version"
    api "com.apollographql.apollo:apollo-android-support:$apollo_version"
    androidTestImplementation "com.apollographql.apollo:apollo-espresso-support:$apollo_version"

    def dagger_version = '2.16'

    api "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    api "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    debugApi 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    releaseApi 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'

    api 'com.jakewharton.timber:timber:4.7.1'

    def okhttp3_version = '3.12.0'
    api "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttp3_version"

    api 'com.squareup.picasso:picasso:2.71828'

    api 'com.google.android.exoplayer:exoplayer:2.7.3'

    implementation "com.airbnb.android:lottie:2.5.5"

    implementation "com.google.firebase:firebase-core:16.0.1"
}

tasks.register("updateSchema", Exec) {
    group = 'GraphQL'
    description = 'Fetches the latest schema from our dev GraphQL-server'

    commandLine 'apollo', 'service:download', '--endpoint=https://graphql.dev.hedvigit.com/graphql', "$projectDir/src/main/graphql/com/hedvig/android/owldroid/schema.json"
}
