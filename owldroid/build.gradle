apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'com.apollographql.android'


apollo {
    generateModelBuilder = true
    customTypeMapping = [
            "URL": "java.lang.String"
    ]
}

apply plugin: 'com.jaredsburrows.license'

licenseReport {
    copyHtmlReportToAssets = true
}

android {
    compileSdkVersion 28
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    def support_version = "28.0.0"

    api "com.android.support:appcompat-v7:$support_version"
    api "com.android.support:exifinterface:$support_version"
    api "com.android.support:support-media-compat:$support_version"
    api "com.android.support:support-v4:$support_version"
    api 'com.android.support.constraint:constraint-layout:1.1.3'
    api 'com.android.support:design:28.0.0'

    def navigation_version = '1.0.0'
    api "android.arch.navigation:navigation-fragment:$navigation_version"
    api "android.arch.navigation:navigation-ui:$navigation_version"
    api "android.arch.navigation:navigation-runtime-ktx:$navigation_version"


    def lifecycle_version = "1.1.0"
    api "android.arch.lifecycle:extensions:$lifecycle_version"
    kapt "android.arch.lifecycle:compiler:$lifecycle_version"

    api "com.shopify:livedata-ktx:1.0.1"
    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
    api 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    api 'nl.littlerobots.rxlint:rxlint:1.7.3'

    testImplementation 'junit:junit:4.12'

    api "com.apollographql.apollo:apollo-runtime:$apollo_version"
    api "com.apollographql.apollo:apollo-android-support:$apollo_version"
    api "com.apollographql.apollo:apollo-rx2-support:$apollo_version"
    androidTestImplementation "com.apollographql.apollo:apollo-espresso-support:$apollo_version"

    def dagger_version = '2.16'

    api "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    api "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    debugApi 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    releaseApi 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'

    api 'com.jakewharton.timber:timber:4.7.1'

    def okhttp3_version = '3.12.0'
    api "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttp3_version"

    api 'com.squareup.picasso:picasso:2.71828'

    api 'com.google.android.exoplayer:exoplayer:2.7.3'

    implementation "com.airbnb.android:lottie:2.5.5"

    api "com.google.firebase:firebase-core:16.0.7"
    api "com.google.firebase:firebase-dynamic-links:16.1.7"
    api "com.google.firebase:firebase-firestore:18.1.0"
    api 'com.google.firebase:firebase-config:16.4.0'

    implementation "com.github.bumptech.glide:glide:4.8.0"
    api 'com.ice.restring:restring:1.0.0'
}

apply plugin: 'com.hedvig.textkeys'

textkeys {
    graphqlEndpoint = 'https://api-euwest.graphcms.com/v1/cjmawd9hw036a01cuzmjhplka/master'
    defaultLanguageCode = 'sv_SE'
    excludedLanguages = ['en_SE']
}

task updateSchema(type: Exec) {
    group = 'GraphQL'
    description = 'Fetches the latest schema from our dev GraphQL-server'

    commandLine 'apollo', 'schema:download', '--endpoint=https://graphql.dev.hedvigit.com/graphql', "$projectDir/src/main/graphql/com/hedvig/android/owldroid/schema.json"
}
